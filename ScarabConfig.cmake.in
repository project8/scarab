# ScarabConfig.cmake

get_filename_component( Scarab_CMAKE_DIR "${CMAKE_CURRENT_LIST_FILE}" PATH )

# Find the dependencies
include( CMakeFindDependencyMacro )
find_dependency( Boost 1.46 REQUIRED COMPONENTS @Scarab_BOOST_COMPONENTS@ )
if( @Scarab_BUILD_CODEC_JSON@ ) #Scarab_BUILD_CODEC_JSON
    find_dependency( RapidJSON REQUIRED )
    if(NOT TARGET rapidjson)
        add_library(rapidjson INTERFACE IMPORTED)
        set_property(TARGET rapidjson PROPERTY  INTERFACE_INCLUDE_DIRECTORIES ${RapidJSON_INCLUDE_DIRS})
    endif()
endif()
if( @Scarab_BUILD_CODEC_YAML@ ) # Scarab_BUILD_CODEC_YAML
    find_dependency( yaml-cpp REQUIRED )
endif()
if( @Scarab_BUILD_PYTHON@ ) # Scarab_BUILD_PYTHON
    find_dependency( Python REQUIRED COMPONENTS Development )
endif()

# Import library targets if it's not already present (usable at build time or as installed)
if( NOT TARGET Scarab::@Scarab_FULL_TARGET_NAME@ )
    if( TARGET @Scarab_FULL_TARGET_NAME@ )
        add_library( Scarab::@Scarab_FULL_TARGET_NAME@ ALIAS @Scarab_FULL_TARGET_NAME@ )
    else()
        include( "${Scarab_CMAKE_DIR}/Scarab_Library_Targets.cmake" )
    endif()
endif()
